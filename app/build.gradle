apply plugin: 'com.android.application'

def getVersion(){
    def cmd = 'git describe --tags'
    Process process = cmd.execute()
    String[] descriptionInfo = process.in.text.split('-')
    if (descriptionInfo.length == 1) {
        descriptionInfo[0] = descriptionInfo[0].replace('\n', '')
        return descriptionInfo[0]+"."+0
    }
    return descriptionInfo[0] + "." + descriptionInfo[1]
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.simonk.project.ppoproject"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName getVersion()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${rootProject.name}-${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "stage"
    productFlavors {
        dev {
            dimension "stage"
            versionNameSuffix "-dev"
            manifestPlaceholders.screenOrientation = "unspecified"
        }
        master {
            dimension "stage"
            manifestPlaceholders.screenOrientation = "portrait"
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    def nav_version = "1.0.0-alpha07"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}
